@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-88e19fe48242bef40132ba7c914b086d> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  sh:select """# Dictionnaire d'images allemand pour les jeunes enfants
# NB: lorsque 2 images ou plus sont produites pour le même lemme, il manque un rang préféré pour le Q-élément.
# SVP corriger cela si vous savez comment.
# Si des termes inappropriés apparaissent, c'est probablement parce que le sens ne possède pas un P5137 (concept) correct.
# (par So9q)
#defaultView:ImageGrid
SELECT DISTINCT ?lexemeId ?lemma ?q_concept ?q_conceptDescription ?picture
WHERE {
  ?lexemeId dct:language wd:Q188; # change language here
            wikibase:lemma ?lemma;
            ontolex:sense ?sense.
  ?sense wdt:P5137 ?q_concept.
  ?q_concept wdt:P18 ?picture.
  ############
  # Exclusions
  ############
  # Exclure les concepts hors-sujet.
  MINUS {?q_concept wdt:P31 wd:Q3624078.}. # countries

  # Exclure les concepts inappropriés.
  VALUES ?minus {
  wd:Q47092 # viol
  wd:Q198   # guerre
  wd:Q124490 # violence
  wd:Q170382 # révolver
  wd:Q1576   # cigare
  #...  .. ajouter ici vos propres exclusions ..
  }.
  MINUS {?sense wdt:P5137 ?minus.}.

  # Exclure les sens non adaptés aux jeunes enfants:
  VALUES ?filter {
  wd:Q8102
  wd:Q545779
  wd:Q1521634
  wd:Q184439}.
  FILTER NOT EXISTS {?sense wdt:P6191 ?filter.}.
  # Si vous souhaitez ne voir que certains éléments, vous pouvez insérer ici une clause FILTER
  # avec expression régulière, comme dans l'exemple Lexème en anglais ci-dessus.
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en,mul\". }
}
ORDER BY lcase(?lemma) # order a-z""";
  rdfs:comment "TODO"@en;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:SPARQL_query_service/queries/examples/fr>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes <https://example.org/to_decide/wikidata_prefixes>;
  schema:target <https://query.wikidata.org/sparql> .
