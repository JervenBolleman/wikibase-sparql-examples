@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-a3b5c3bbfc898f892a242f86dc87e9aa> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  rdfs:comment "TODO"@en;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:WikiProject_Antiquity>,
    <https://www.wikidata.org//wiki/Wikidata:WikiProject_Ancient_Greece/Queries>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes <https://example.org/to_decide/wikidata_prefixes>;
  schema:target <https://query.wikidata.org/sparql>;
  sh:select """PREFIX gas: <http://www.bigdata.com/rdf/gas#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX bd: <http://www.bigdata.com/rdf#>
#defaultView:Graph
SELECT DISTINCT ?philosopher ?philosopherLabel ?rgb ?student ?studentLabel 
WHERE {
  SERVICE gas:service {
    gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\";
    gas:in wd:Q913;
    gas:linkType wdt:P802;
    gas:traversalDirection \"Undirected\";
    gas:out ?philosopher ;
    gas:out1 ?depth ;
    gas:out2 ?student . 
  }
   ?philosopher wdt:P106 wd:Q4964182 . #restricts the query to students that ended up philosophers themselves
   ?philosopher wdt:P135 ?school .
        BIND (
        COALESCE(
          IF(?school = wd:Q193589, \"FF1700\", 1/0),     # platonism
          IF(?school = wd:Q194414, \"33FF3B\", 1/0),     # peripatetician
          IF(?school = wd:Q1126340, \"BEFAFF\", 1/0),    # pyrrhonism
          IF(?school = wd:Q179541, \"C2CFFF\", 1/0),     # epicurism
          IF(?school = wd:Q1395219, \"134d83\", 1/0),    # scepticism
          IF(?school = wd:Q48235, \"57FFD2\", 1/0),      # stoicism
          IF(?school = wd:Q1145454, \"19A2A2\", 1/0),    # megarism
          IF(?school = wd:Q750192, \"F5FE66\", 1/0),     # cyrenaism
          IF(?school = wd:Q485459, \"D69A00\", 1/0),     # cynism
          IF(?school = wd:Q1350056, \"35A219\", 1/0),    # eretrian school
          IF(?school = wd:Q191359, \"976031\", 1/0),     # pythagorism
          IF(?school = wd:Q214577, \"913194\", 1/0),     # eleatics
          IF(?school = wd:Q178540, \"140714\", 1/0),     # presocratics (same as pluralists)
          IF(?school = wd:Q1275113, \"140714\", 1/0),    # pluralists (same as presocratics
          IF(?school = wd:Q178668, \"d8a31c\", 1/0),     # atomists
          IF(?school = wd:Q3345153, \"1c51d8\", 1/0),    # New Academy
          \"FFFFFF\"
        ) AS ?rgb 
      )
  
 SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}""" .
