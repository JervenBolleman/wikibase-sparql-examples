@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-08e37f988d552505c8dfc814322ae344> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  rdfs:comment "TODO"@en;
  dcterms:isPartOf <https://www.wikidata.org//wiki/User:Taravyvan_Adijene/SPARQL>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes <https://example.org/to_decide/wikidata_prefixes>;
  schema:target <https://query.wikidata.org/sparql>;
  sh:select """PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX schema: <http://schema.org/>
PREFIX bd: <http://www.bigdata.com/rdf#>
# асобы, якія нарадзіліся на тэрыторыі Пружанскага раёну
SELECT ?item ?itemLabel ?pic ?birthDate ?pobLabel ?coord ?statements (COUNT(DISTINCT ?sitelink) AS ?linkshere) ?VIAF ?IMDb WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],be-tarask\". }   # фарматаваньне метак вываду
  BIND(xsd:integer(STRAFTER(STR(?item), \"Q\")) AS ?qid)                                        
  ?item wikibase:statements ?statements.                                                      # дзеля падліку сьцьверджаньняў
  OPTIONAL {
    ?sitelink schema:about ?item.
    ?sitelink schema:inLanguage ?lang.
  }                                                                                           # дзеля падліку спасылак на элемэнт
  ?item wdt:P31 wd:Q5.                                                                        # толькі людзі
  { ?item wdt:P19 wd:Q1885543. }                                                              # месца нараджэньня — Пружанскі раён
  UNION
  {
    ?item wdt:P19 ?pob.
    ?pob wdt:P131* wd:Q1885543.
  }                                                                                           # альбо месца нараджэньня знаходзіцца ў Пружанскім раёне
  OPTIONAL { ?item wdt:P569 ?birthDate. }
  OPTIONAL { ?item wdt:P19 ?pob. }
  OPTIONAL { ?item wdt:P18 ?pic. }
  OPTIONAL { ?item wdt:P214 ?VIAF. }
  OPTIONAL { ?item wdt:P345 ?IMDb. }
  OPTIONAL { ?pob wdt:P625 ?coord. }
}
GROUP BY ?item ?statements ?itemLabel ?pic ?birthDate ?pobLabel ?coord ?VIAF ?IMDb
ORDER BY DESC(?linkshere)""";
  <https://purl.expasy.org/sparql-examples/ontology#federatesWith> <http://wikiba.se/ontology#label> .
