@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-8ae1bd75f822105a339f3321199d1d88> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  sh:select """# Topics through time

PREFIX target: <http://www.wikidata.org/entity/Q3382012>

SELECT
  ?year
  ?works
  ?topic ?topicLabel
WITH {
  SELECT 
    ?work
  WHERE {
    ?work wdt:P1433 target: .
  }
} AS %works
WITH {
  SELECT
    (COUNT(?work) AS ?count)
    ?topic
  WHERE {
    INCLUDE %works
    ?work wdt:P921 ?topic .
  }
  GROUP BY ?topic
  ORDER BY DESC(?count)
  LIMIT 5
} AS %topics
WITH {
  SELECT
    ?year 
    (COUNT(?work) AS ?works)
    ?topic
  WHERE {
    INCLUDE %works
    INCLUDE %topics
    ?work wdt:P921 ?topic ;
          wdt:P577 ?publication_datetime .
    BIND(STR(YEAR(?publication_datetime)) AS ?year)
  }
  GROUP BY ?year ?topic
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
ORDER BY ?year""";
  rdfs:comment "TODO"@en;
  dcterms:isPartOf <https://www.wikidata.org//wiki/User:Will_(Wiki_Ed)/APS_Project_Page/Queries>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes <https://example.org/to_decide/wikidata_prefixes>;
  schema:target <https://query.wikidata.org/sparql> .
